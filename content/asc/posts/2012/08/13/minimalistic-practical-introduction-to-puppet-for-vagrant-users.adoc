{:title
 "Minimalistic Practical Introduction to Puppet (Not Only) For Vagrant Users",
 :date "2012-08-13",
 :layout :post,
 :tags ["DevOps"],
 :tags-orig ["DevOps" "introduction" "puppet"],
 :categories ["General" "Tools"],
 :extra-css #{}
}

++++
I couldn't find any good, brief, practical introduction into Puppet that gives you basic working knowledge in minimal time, so here it is. You will learn how to do the elementary things with Puppet - install packages, copy files, start services, execute commands. I won't go into Puppet installation, nodes, etc. as this introduction focuses on the users of <a href="https://vagrantup.com/">Vagrant</a>, which comes with Puppet pre-installed and working in the serverless configuration.<br><br><!--more-->
<h2>What is Puppet?</h2>
Puppet is a <em>provisioner</em> - a cross-OS software that sets up operating systems by installing and configuring software etc., based on some form of instructions. Here as an example of such instructions - a <em>manifest</em> - for Puppet:<br><br><pre><code>
# my_sample_manifest.pp
class my_development_env {
  package {'vim': ensure =&gt; 'present' }
}<br><br># Apply it
include my_development_env
</code></pre><br><br>Running it with
<pre>puppet apply --verbose --debug my_sample_manifest.pp</pre>
would install vim on the system.<br><br>Notice that while Puppet can be run only once, it is generally intended to be run repeatedly to fetch and apply the latest configuration (usually from some source code management system such as Git). Therefore all its operations must be idempotent - they can be performed safely multiple times.
<h2>The Puppet Configuration File a.k.a. Manifest</h2>
Puppet manifests are written in a Ruby-like syntax and are composed of the declaration of <em>"resources"</em> (packages, files, etc.), grouped optionally into one or more <em>classes</em> (i.e. templates that can be applied to a system). Each concrete resource has a title (e.g. 'vim') followed by a colon and comma-separated pairs of property =&gt; value. You may have multiple resources of the same type (e.g. package) as long as they have different titles.<br><br>The property values are most often strings, enclosed by 'single quotes' or alternatively with "double quotes" if you want variables within them replaced with their values. (A variable starts with the dollar sign.)<br><br>Instead of a name or a value you can also use an array of titles/values, enclosed with [  ].<br><br>(Note: It is a common practice to leave a trailing comma behind the last property =&gt; value pair.)<br><br>You can group resources within classes (<em>class my_class_name { ... }</em>) and then apply the class with either include (<em>include my_class_name</em>) or with the more complex class (<em>class { 'my_class_name': }</em>). You can also include a class in another class like this.
<h2>Doing Things With Puppet</h2>
<h3>Installing Software With Package</h3>
The most used way to install software packages is with this simple usage of <a href="https://docs.puppetlabs.com/references/latest/type.html#package">package</a>:<br><br><pre><code>
package {['vim', 'apache2']: ensure =&gt; 'present' }
</code></pre><br><br>Puppet supports various package providers and by default picks the system one (such as apt at Ubuntu or rpm at RedHat). You can explicitly state another supported package provider such as Ruby's gem or Python's pip.<br><br>You can also request a particular version of the package (if supported) with ensure =&gt; '&lt;version&gt;' or to the latest one with ensure =&gt; 'latest' (this will also reinstall it whenever a new version is released and Puppet runs). In the case of ensure =&gt; 'present' (also called 'installed'): if the package already is installed then nothing happens otherwise the latest version is installed.
<h3>Copying and creating Files With File</h3>
Create a file with content specified in-line:<br><br><pre><code>
file {'/etc/myfile.example':
  ensure =&gt; 'file',
  content =&gt; &quot;line1\nline2\n&quot;,
}
</code></pre><br><br>Copy a directory including its content, set ownership etc.:<br><br><pre><code>
file {'/etc/apache2':
  ensure  =&gt; 'directory',
  source  =&gt; '/vagrant/files/etc/apache2',
  recurse =&gt; 'remote',
  owner   =&gt; 'root',
  group   =&gt; 'root',
  mode    =&gt; '0755',
}
</code></pre><br><br>This requires that the directory /vagrant/files/etc/apache2 exists. (Vagrant automatically shares the directory with the Vagrantfile as /vagrant in the VM so this actually copies files from the host machine.With the master-agent setup of Puppet you can also get files remotely, from the master, using the puppet:// protocol in the source.)<br><br>You can also create files based on ERB <a href="https://docs.puppetlabs.com/guides/templating.html">templates</a> (with source =&gt; template('relative/path/to/it')) but we won't discuss that here.<br><br>You can also create symlinks (with ensure =&gt; link, target =&gt; 'path/to/it') and do other stuff, reader more in the <a href="https://docs.puppetlabs.com/references/latest/type.html#file">file resource documentation</a>.
<h3>(Re)Starting Daemons with Service</h3>
When you've installed the necessary packages and copied their configuration files, you'll likely want to start the software, which is done with <a href="https://docs.puppetlabs.com/references/latest/type.html#service">service</a>:<br><br><pre><code>
service { 'apache2':·
  ensure =&gt; running,·
  require =&gt; Package['apache2'],
}
</code></pre><br><br>(We will talk about require later; it makes sure that we don't try to start Apache before it's installed.)<br><br>On Linux, Puppet makes sure that the service is registered with the system to be started after OS restart and starts it. Puppet reuses the OS' support for services, such as the service startup scripts in /etc/init.d/ (where service = script's name) or Ubuntu's upstart.<br><br>You can also declare your own start/stop/status commands with the properties of the same names, f.ex. start =&gt; '/bin/myapp start'.
<h3>When Everything Fails: Executing Commands</h3>
You can also execute any shell command with <a href="https://docs.puppetlabs.com/references/latest/type.html#exec">exec</a>:<br><br><pre><code>
exec { 'install hive':
  command =&gt; 'wget http://apache.uib.no/hive/hive-0.8.1/hive-0.8.1-bin.tar.gz -O - | tar -xzC /tmp',
  creates =&gt; '/tmp/hive-0.8.1-bin',
  path =&gt; '/bin:/usr/bin',
  user =&gt; 'root',
}
</code></pre><br><br>Programs must have fully qualified paths or you must specify where to look for them with <em>path</em>.<br><br>It is critical that all such commands can be run multiple times without harm, i.e., they are idempotent. To achieve that you can instruct Puppet to skip the command if a file exists with <em>creates</em> =&gt; ... or if a command succeeds or fails with <em>unless</em>/<em>onlyif</em>.<br><br>You can also run a command in reaction to a change to a dependent object by combining <em>refreshonly</em> and <em>subscribe</em>.
<h3>Other Things to Do</h3>
You can create users and groups, register authorized ssh keys, define cron entries, mount disks and much more - check out <a href="https://docs.puppetlabs.com/references/latest/type.html">Puppet Type Reference</a>.
<h3>Enforcing Execution Order With Require, Before, Notify etc.</h3>
Puppet processes the resources specified in a random order, not in the order of specification. So if you need a particular order - such as installing a package first, copying config files second, starting a service third - then you must tell Puppet about these dependencies. There are multiple ways to express dependencies and several types of dependencies:
<ul>
	<li>Before and require - simple execution order dependency</li>
	<li>Notify and subscribe - an enhanced version of before/require which also notifies the dependent resource whenever the resource it depends on changes, used with refreshable resources such as services; typically used between a service and its configuration file (Puppet will refresh it by restarting it)</li>
</ul>
Ex.:<br><br><pre><code>
service { 'apache2':
  ensure =&gt; running,
  subscribe =&gt; File['/etc/apache2'],
  require =&gt; [ Package['apache2'], File['some/other/file'] ],
}
</code></pre><br><br>Notice that contrary to resource <em>declaration</em> the resource <em>reference</em> has the resource name uppercased and the resource title is within [].<br><br>Puppet is clever enough to derive the "require" dependency between some resource that it manages such as a file and its parent folder or an exec and its user - this is well documented for each resource in the <a href="docs.puppetlabs.com/references/latest/type.html">Puppet Type Reference</a> in the paragraphs titled "Autorequires:".<br><br>You can also express dependencies between individual classes by defining <a href="https://www.personal.psu.edu/ryc108/blogs/puppetmaster/2010/10/automating-shibboleth-idp-builds-using-stages.html">stages</a>, assigning selected classes to them, and declaring the ordering of the stages using before &amp; require. Hopefully you won't need that.
<h2>Bonus Advanced Topic: Using Puppet Modules</h2>
Modules are self-contained pieces of Puppet configuration (manifests, templates, files) that you can easily include in your configuration by placing them into Puppet's manifest directory. Puppet automatically find them and makes their classes available to you for use in your manifest(s). You can download modules from the <a href="https://forge.puppetlabs.com/">Puppet Forge</a>.<br><br><a href="https://forge.puppetlabs.com/puppetlabs/mysql">See the examples on the puppetlabs/mysql module page</a> about how such a module would be used in your manifest.<br><br>With Vagrant you would instruct Vagrant to provide modules from a particular directory available to Puppet with<br><br><pre><code>
config.vm.provision :puppet,
  :module_path =&gt; &quot;my_modules&quot; do |puppet|
        puppet.manifest_file = &quot;my_manifest.pp&quot;
end
</code></pre><br><br>(in this case you'd need manifest/ next to your Vagrantfile) and then in your Puppet manifest you could have <em>class { 'mysql': }</em> etc.
<h2>Where to Go Next?</h2>
There are some things I haven't covered that you're likely to encounter such as <a href="https://docs.puppetlabs.com/learning/variables.html">variables and conditionals</a>, built-in <a href="https://docs.puppetlabs.com/references/latest/function.html">functions</a> such as template(..), <a href="https://docs.puppetlabs.com/guides/parameterized_classes.html">parametrized classes</a>, class inheritance. I have also skipped all master-agent related things such as nodes and facts. It's perhaps best to learn them when you encounter them.<br><br>In each case you should have a look at the <a href="https://docs.puppetlabs.com/references/latest/type.html">Puppet Type Reference</a> and if you have plenty of time, you can start reading the <a href="https://docs.puppetlabs.com/guides/language_guide.html">Language Guide</a>. In the on-line <a href="https://www.puppetcookbook.com/">Puppet CookBook</a> you can find many useful snippets. You may also want to download the <a href="https://docs.puppetlabs.com/learning/#get-equipped">Learning Puppet VM</a> to experiment with Puppet (or just <a href="https://github.com/holyjak/presentations/tree/master/CommitOnDayOneThanksToVagrantAndPuppet">try Vagrant</a>).
++++
